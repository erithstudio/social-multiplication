import org.apache.tools.ant.filters.*

plugins {
    id 'org.springframework.boot' version '2.5.0'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'org.asciidoctor.jvm.convert' version '3.3.0'
}

group = 'microservices.template'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'
description = 'multiplication app'

ext {
    set('app.artifactId', project.getName())
    set('app.description', project.getDescription())
    set('app.version', project.getVersion())
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    implementation 'org.springframework.cloud:spring-cloud-starter-config:3.0.4'
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client:3.0.3'
    implementation 'de.codecentric:spring-boot-admin-starter-client:2.4.1'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.6'
    implementation 'org.thepavel:spring-icomponent:1.0.8'

    implementation 'com.h2database:h2'

    implementation "net.rakugakibox.spring.boot:orika-spring-boot-starter:1.9.0"
    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'

    testCompileOnly 'org.projectlombok:lombok:1.18.20'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.20'

    implementation project(':social-common')
}

processResources {
    ant.println("projectProperties: " + project.ext.getProperties())
    filesMatching('**/application.properties') {
        filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: project.ext.getProperties())
    }
}

asciidoctor {
    baseDirFollowsSourceFile()
    sourceDir  file("$rootDir/docs/asciidoc")

    attributes 'build-gradle': file('build.gradle'),
            // 'baseDir': "${rootProject.rootDir}",
            // 'sourcedir': project.sourceSets.main.java.srcDirs[0],
            'snippets': file('target/snippets'),
            'generate_snippets': file('target/generated-snippets'),
            'generate-source-snippets': file('target/generated-sources/snippets')
}